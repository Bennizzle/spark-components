<?xml version="1.0" encoding="utf-8"?>
<s:Group 
    xmlns:fx="http://ns.adobe.com/mxml/2009" 
    xmlns:s="library://ns.adobe.com/flex/spark" 
    xmlns:mx="library://ns.adobe.com/flex/mx"
    creationComplete="init()"
>
    
    <fx:Script>
        <![CDATA[
            import mx.binding.utils.BindingUtils;
            import mx.binding.utils.ChangeWatcher;
            import mx.core.IVisualElement;
            import mx.core.UIComponent;
            import mx.events.FlexEvent;
            
            private var _slider:Object;    
            
            public function get slider():Object
            {
                return _slider;
            }
            
            public function set slider(slider:Object):void
            {
                _slider = slider;
                
                var min:Number = _slider.minimum;
                var max:Number = _slider.maximum;
                var snap:Number = _slider.snapInterval;
                
                BindingUtils.bindProperty(slider, "minimum", minInput, "value");
                BindingUtils.bindProperty(slider, "maximum", maxInput, "value");
                BindingUtils.bindProperty(slider, "snapInterval", snapInput, "value");
                
                BindingUtils.bindSetter(updateValueDisplay, slider, valueField);
                
                slider.minimum = min;
                slider.maximum = max;
                slider.snapInterval = snap;
                
                updateModifierValues();
                
                addElement(slider as IVisualElement);
            }
            
            protected function get valueField():String
            {
                return "value";
            }
            
            private function init():void
            {
                addInputListeners();
                
                liveDraggingCheckbox.selected = (slider as UIComponent).getStyle("liveDragging");
                liveDraggingCheckbox.addEventListener(Event.CHANGE, liveDraggingChangeHandler);
                
                //updateValueDisplay();
            }
            
            protected function addInputListeners():void
            {
                minInput.addEventListener(Event.CHANGE, changeHandler);
                maxInput.addEventListener(Event.CHANGE, changeHandler);
                snapInput.addEventListener(Event.CHANGE, changeHandler);   
                
                (slider as IEventDispatcher).addEventListener(Event.CHANGE, changeHandler);
            }
            
            protected function liveDraggingChangeHandler(event:Event):void
            {
                (slider as UIComponent).setStyle("liveDragging", liveDraggingCheckbox.selected);
            }
            
            protected function changeHandler(event:Event):void
            {
                updateModifierValues();               
            }    
              
            protected function updateModifierValues():void
            {
                minInput.value = slider.minimum;
                maxInput.value = slider.maximum;
                snapInput.value = slider.snapInterval;    
             
                //updateValueDisplay();
            }
            
            private function updateValueDisplay(value:Object):void
            {
                valueDisplay.text = "Value(s): " + slider[valueField];                
            }
            
        ]]>
    </fx:Script>

    <s:layout>
        <s:VerticalLayout 
            paddingTop="10" paddingLeft="10" paddingRight="10" paddingBottom="10"
            gap="20"
            />
    </s:layout>
    
    <s:HGroup id="contentGroup">
        <s:VGroup>
            <s:Label text="Minimum:" />
            <s:NumericStepper id="minInput" value="0" maximum="100" />            
        </s:VGroup>
        <s:VGroup>
            <s:Label text="Maximum:" />
            <s:NumericStepper id="maxInput" value="10" maximum="100" />            
        </s:VGroup>
        <s:VGroup>
            <s:Label text="Snap interval:" />
            <s:NumericStepper id="snapInput" value="1" maximum="100" />            
        </s:VGroup>    
        <s:VGroup>
            <s:Label text="Live Dragging:" />
            <s:CheckBox id="liveDraggingCheckbox" selected="true" />           
        </s:VGroup>    
    </s:HGroup>      
    
    <s:Label id="valueDisplay" />
</s:Group>

