<?xml version="1.0" encoding="utf-8"?>
<s:Group 
    xmlns:fx="http://ns.adobe.com/mxml/2009" 
    xmlns:s="library://ns.adobe.com/flex/spark" 
    xmlns:mx="library://ns.adobe.com/flex/mx"
    creationComplete="init()"
>
    
    <fx:Script>
        <![CDATA[
            import mx.binding.utils.BindingUtils;
            import mx.core.IVisualElement;
            import mx.events.FlexEvent;
            
            public var valueField:String = "value";
            
            private var _slider:Object;
                    
            public function set slider(slider:Object):void
            {
                _slider = slider;
                
                var min:Number = _slider.minimum;
                var max:Number = _slider.maximum;
                var snap:Number = _slider.snapInterval;
                
                BindingUtils.bindProperty(_slider, "minimum", minInput, "value");
                BindingUtils.bindProperty(_slider, "maximum", maxInput, "value");
                BindingUtils.bindProperty(_slider, "snapInterval", snapInput, "value");
                
                _slider.minimum = min;
                _slider.maximum = max;
                _slider.snapInterval = snap;
                
                updateModifierValues();
                
                addElement(slider as IVisualElement);
            }
            
            private function init():void
            {
                minInput.addEventListener(Event.CHANGE, changeHandler);
                maxInput.addEventListener(Event.CHANGE, changeHandler);
                snapInput.addEventListener(Event.CHANGE, changeHandler);
                
                (_slider as IEventDispatcher).addEventListener(FlexEvent.UPDATE_COMPLETE, changeHandler);
                
                updateValueDisplay();
            }
            
            private function changeHandler(event:Event):void
            {
                updateModifierValues();               
            }    
              
            private function updateModifierValues():void
            {
                minInput.value = _slider.minimum;
                maxInput.value = _slider.maximum;
                snapInput.value = _slider.snapInterval;     
             
                updateValueDisplay();
            }
            
            private function updateValueDisplay():void
            {
                valueDisplay.text = "Value(s): " + _slider[valueField];                
            }
            
        ]]>
    </fx:Script>

    <s:layout>
        <s:VerticalLayout 
            paddingTop="10" paddingLeft="10" paddingRight="10" paddingBottom="10"
            gap="20"
            />
    </s:layout>
    
    <s:HGroup>
        <s:VGroup>
            <s:Label text="Minimum:" />
            <s:NumericStepper id="minInput" value="0" maximum="100" />            
        </s:VGroup>
        <s:VGroup>
            <s:Label text="Maximum:" />
            <s:NumericStepper id="maxInput" value="10" maximum="100" />            
        </s:VGroup>
        <s:VGroup>
            <s:Label text="Snap interval:" />
            <s:NumericStepper id="snapInput" value="1" maximum="100" />            
        </s:VGroup>          
    </s:HGroup>      
    
    <s:Label id="valueDisplay" />
</s:Group>

